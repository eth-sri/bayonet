parameters{ NUM_PACKETS(3) }

num_steps 20;

query probability(arrived@H1 == 1);

topology{
  nodes{ H0, H1, S0, S1, S2, S3, S4, S5, S6, S7, S8, S9, S10 }
  links{
    (H0,pt1) <-> (S9,pt3),
    (S9,pt1) <-> (S0,pt1),
    (S9,pt2) <-> (S3,pt1),
    (S0,pt2) <-> (S7,pt1),
    (S0,pt3) <-> (S1,pt1),
    (S1,pt4) <-> (S2,pt2),
    (S1,pt2) <-> (S6,pt3),
    (S1,pt3) <-> (S8,pt3),
    (S2,pt1) <-> (S3,pt2),
    (S2,pt3) <-> (S4,pt1),
    (S4,pt2) <-> (S5,pt1),
    (S5,pt2) <-> (S6,pt1),
    (S6,pt2) <-> (S10,pt1),
    (S7,pt2) <-> (S8,pt1),
    (S8,pt2) <-> (S10,pt3),
    (S10,pt2) <-> (H1,pt1)
  }
}
queue_capacity 2;

packet_fields{ }

programs{ H0 -> relsrc, H1 -> reldst, S0 -> swch1, S1 -> swch1, S2 -> swch2, S3 -> fwdr, S4 -> fwdr, S5 -> fwdr, S6 -> badfwdr, S7 -> fwdr, S8 -> fwdr, S9 -> swch3, S10 -> fwdr }

def relsrc(pkt, port) {
  fwd(1);
}

def reldst(pkt, port) state arrived(0) {
  arrived=1;
  drop;
}

def fwdr(pkt, port) {
  fwd(2);
}

def badfwdr(pkt, port) state failing(2) {
  if failing == 2 {
    failing = flip(1/1000);
  }
  if failing == 1 {
    drop;
  } else {
    fwd(2);
  }
}

def swch1(pkt, port) {
  fwd(flip(1/2)+2);
}

def swch2(pkt, port) {
  fwd(flip(3/7)+2);
}

def swch3(pkt, port) {
  fwd(flip(4/9)+1);
}

def scheduler(){
	actions := ([]: (R x R)[]);
	for i in [0..k){
		if (Q_in@i).size() > 0 { actions ~= [(RunSw,i)]; }
		if (Q_out@i).size() > 0 { actions ~= [(FwdQ,i)]; }
	}
	return actions[uniformInt(0,actions.length-1)];
}
